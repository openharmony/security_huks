# Copyright (C) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/security/huks/build/config.gni")
import("//base/security/huks/huks.gni")
import("//build/ohos.gni")

config("huks_config") {
  include_dirs = [ "ipc" ]
}

if (os_level == "standard") {
  ohos_static_library("libhuks_service_idl_standard_static") {
    subsystem_name = "security"
    part_name = "huks"
    public_configs = [ ":huks_config" ]

    include_dirs = [ 
      "//base/security/huks/services/huks_standard/huks_service/main/os_dependency/idl/passthrough",
      "//base/security/huks/services/huks_standard/huks_service/main/extension_plugin_manger/inc",
    ]

    sources = [
      "ipc/hks_ipc_service.c",
      "ipc/hks_permission_check.cpp",
      "ipc/hks_response.cpp",
      "ipc/hks_service_ipc_serialization.c",
      "ipc/hks_ipc_service_provider_adapter.cpp",     
      "ipc/hks_ipc_service_provider.cpp",
    ]

    if (huks_enable_hdi_in_standard) {
      sources += [ "passthrough/huks_hdi_access.c" ]
    } else {
      sources += [
        "passthrough/huks_access.c",
        "passthrough/huks_core_dynamic_hal.c",
      ]
    }

    deps = [
      "//base/security/huks/frameworks/huks_standard/main/common:libhuks_common_standard_static",
      "//base/security/huks/services/huks_standard/huks_service/main/core:libhuks_service_core_standard_static",
      "//base/security/huks/services/huks_standard/huks_service/main/systemapi_wrap/at_wrapper:libhuks_at_systemapi_wrap_static",
      # "//base/security/huks/services/huks_standard/huks_service/main/extension_plugin_manger:libhuks_ukey_plugin_extesnion_static",
      "//base/security/huks/services/huks_standard/huks_service/extension/ukey/common:libhuks_ukey_common_static"
    ]

    if (enable_bundle_framework) {
      deps += [ "//base/security/huks/services/huks_standard/huks_service/main/systemapi_wrap/bms:libhuks_bms_systemapi_wrap_static" ]
    }

    complete_static_lib = true

    branch_protector_ret = "pac_ret"

    sanitize = {
      integer_overflow = true
      cfi = true
      debug = false
      cfi_cross_dso = true
      boundary_sanitize = true
      ubsan = true
    }

    external_deps = [
      "access_token:libaccesstoken_sdk",
      "access_token:libtokenid_sdk",
      "c_utils:utils",
      "ipc:ipc_single",
    ]

    defines = []

    if (has_os_account_part) {
      external_deps += [ "os_account:os_account_innerkits" ]
      defines += [ "HAS_OS_ACCOUNT_PART" ]
    }

    configs = [
      "//base/security/huks/frameworks/config/build:l2_standard_common_config",
    ]
    external_deps += [ "hilog:libhilog" ]

    if (huks_enable_hdi_in_standard) {
      external_deps += [ "drivers_interface_huks:libhuks_proxy_1.1" ]
    }
  }
} else {
  ohos_static_library("libhuks_service_idl_small_static") {
    public_configs = [ ":huks_config" ]

    include_dirs = [
      "//base/security/huks/services/huks_standard/huks_service/main/os_dependency/idl/passthrough",
      "//base/security/huks/services/huks_standard/huks_service/main/os_dependency/sa/sa_mgr",
      "//base/security/huks/frameworks/huks_standard/main/os_dependency/ipc/include",
      "//base/security/huks/services/huks_standard/huks_service/main/extension_plugin_manger/inc",
    ]
    configs = [
      "//base/security/huks/frameworks/config/build:l1_small_common_config",
    ]

    sources = [
      "ipc/hks_ipc_service.c",
      "ipc/hks_response_samgr.c",
      "ipc/hks_service_ipc_serialization.c",
      "passthrough/huks_access.c",
      "passthrough/huks_core_dynamic_hal.c",
    ]
    deps = [
      "//base/security/huks/frameworks/huks_standard/main/common:libhuks_common_small_static",
      "//base/security/huks/services/huks_standard/huks_service/main/core:libhuks_service_core_small_static",
      "//base/security/huks/services/huks_standard/huks_service/main/extension_plugin_manger:libhuks_ukey_plugin_extesnion_static",
    ]

    complete_static_lib = true

    deps += [ "//foundation/systemabilitymgr/samgr_lite/samgr:samgr" ]
    external_deps = [ "hilog_lite:hilog_shared" ]
  }
}
