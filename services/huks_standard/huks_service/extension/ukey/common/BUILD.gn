# Copyright (C) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/security/huks/build/config.gni")
import("//build/config/components/idl_tool/idl.gni")
import("//build/ohos.gni")

idl_gen_interface("IHuks_Access_Ext_Base") {
  src_idl = rebase_path("IHuksAccessExtBase.idl")
}

config("IHuks_Access_Ext_Base_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "//base/security/huks/interfaces/inner_api/huks_standard/main/include",
    "//base/security/huks/frameworks/huks_standard/main/common/include",
    "${target_gen_dir}",
    "inc",
  ]
}

ohos_source_set("huks_access_ext_base_include") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  public_configs = [ ":IHuks_Access_Ext_Base_config" ]
  output_values = get_target_outputs(":IHuks_Access_Ext_Base")
  sources = filter_include(output_values, [ "*.cpp" ])
  deps = [ ":IHuks_Access_Ext_Base" ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_connect_callback_stub",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "ipc:ipc_single",
  ]
  subsystem_name = "security"
  part_name = "huks"
}

if (os_level == "standard") {
  ohos_static_library("libhuks_ukey_common_static") {
    subsystem_name = "security"
    part_name = "huks"
    configs = [
      "//base/security/huks/frameworks/config/build:l2_standard_common_config",
    ]

    include_dirs = [ "inc" ]

    sources = [ "src/hks_ukey_common.cpp" ]

    public_configs = [ ":IHuks_Access_Ext_Base_config" ]

    deps = [
      ":IHuks_Access_Ext_Base",
      "//base/security/huks/frameworks/huks_standard/main/common:libhuks_common_standard_static",
      "//base/security/huks/frameworks/huks_standard/main/os_dependency:libhuks_mem_standard_static",
    ]
    complete_static_lib = true
    cflags = []

    branch_protector_ret = "pac_ret"

    sanitize = {
      integer_overflow = true
      cfi = true
      debug = false
      cfi_cross_dso = true
      boundary_sanitize = true
      ubsan = true
    }

    external_deps = [
      "bundle_framework:appexecfwk_base",
      "bundle_framework:appexecfwk_core",
      "cJSON:cjson",
      "c_utils:utils",
      "hilog:libhilog",
      "ipc:ipc_single",
      "samgr:samgr_proxy",
    ]
  }
}
