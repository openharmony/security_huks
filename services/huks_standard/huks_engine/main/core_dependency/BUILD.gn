# Copyright (C) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/security/huks/build/config.gni")
import("//base/security/huks/huks.gni")
import("//build/ohos.gni")

config("huks_config") {
  include_dirs = [ "include" ]
}

ohos_static_library("libhuks_core_hal_api_static") {
  sanitize = {
    integer_overflow = true
    cfi = true
    debug = false
    cfi_cross_dso = true
    boundary_sanitize = true
    ubsan = true
  }
  branch_protector_ret = "pac_ret"

  subsystem_name = "security"
  part_name = "huks"
  public_configs = [ ":huks_config" ]
  configs = [
    "//base/security/huks/frameworks/config/build:l2_standard_common_config",
  ]
  sources = [
    "src/hks_chipset_platform_key_hardcoded.c",
    "src/hks_core_useriam_wrap.cpp",
  ]

  deps = [ "//base/security/huks/frameworks/huks_standard/main/common:libhuks_common_standard_static" ]
  include_dirs = [
    "include",
    "//base/security/huks/services/huks_standard/huks_engine/main/core/include",
    "//base/security/huks/frameworks/huks_standard/main/common/include",
  ]

  complete_static_lib = true

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
  ]
  if (huks_security_level == "software" && has_huks_hdi_driver &&
      !enable_hks_mock) {
    defines = [ "HKS_CORE_ENABLE_HDI_DRIVER" ]
    external_deps += [ "drivers_interface_user_auth:libuser_auth_proxy_2.0" ]
  } else {
    if (enable_hks_mock) {
      deps += [ "//base/security/huks/services/huks_standard/huks_service/main/systemapi_mock:libhuks_service_systemapi_mock_static" ]
    } else {
      deps += [ "//base/security/huks/services/huks_standard/huks_service/main/systemapi_wrap/useridm:libhuks_service_systemapi_wrap_static" ]
    }
  }
}
