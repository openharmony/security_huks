# Copyright (C) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/security/huks/build/config.gni")
import("//base/security/huks/huks.gni")
import("//build/ohos.gni")

config("huks_config") {
  include_dirs = [ "include" ]
}

if (os_level == "standard") {
  ohos_static_library("libhuks_utils_client_service_adapter_static") {
    branch_protector_ret = "pac_ret"

    sanitize = {
      integer_overflow = true
      cfi = true
      debug = false
      cfi_cross_dso = true
      boundary_sanitize = true
      ubsan = true
    }
    subsystem_name = "security"
    part_name = "huks"
    public_configs = [ ":huks_config" ]
    include_dirs =
        [ "../../frameworks/huks_standard/main/crypto_engine/openssl/include" ]
    defines = [
      "_HARDWARE_ROOT_KEY_",
      "_HUKS_LOG_ENABLE_",
      "OPENSSL_SUPPRESS_DEPRECATED",
    ]
    sources = [
      "hks_client_service_adapter.c",
      "hks_client_service_adapter_common.c",
    ]
    configs = [
      "//base/security/huks/frameworks/config/build:l2_standard_common_config",
    ]
    cflags = [
      "-Wall",
      "-Werror",
    ]
    deps = [
      "//base/security/huks/frameworks/huks_standard/main/common:libhuks_common_standard_static",
      "//base/security/huks/frameworks/huks_standard/main/os_dependency:libhuks_mem_standard_static",
    ]
    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
      "openssl:libcrypto_shared",
    ]
    complete_static_lib = true
  }
} else {
  ohos_static_library("libhuks_utils_client_service_adapter_static") {
    public_configs = [ ":huks_config" ]
    defines = [ "OPENSSL_SUPPRESS_DEPRECATED" ]
    include_dirs =
        [ "../../frameworks/huks_standard/main/crypto_engine/include/" ]
    configs = [
      "//base/security/huks/frameworks/config/build:l1_small_common_config",
    ]

    sources = [
      "hks_client_service_adapter_common.c",
      "hks_client_service_adapter_lite.c",
    ]

    deps = [
      "//base/security/huks/frameworks/huks_standard/main/common:libhuks_common_small_static",
      "//base/security/huks/frameworks/huks_standard/main/crypto_engine:libhuks_crypto_engine_small_static",
      "//base/security/huks/frameworks/huks_standard/main/os_dependency:libhuks_os_dependency_small_static",
    ]

    external_deps = [ "hilog_lite:hilog_shared" ]
    if (huks_dependency_mbedtls_path != "") {
      deps += [ huks_dependency_mbedtls_path ]
    } else {
      deps += [ "//third_party/mbedtls" ]
    }
    if (huks_dependency_openssl_path != "") {
      deps += [ huks_dependency_openssl_path ]
    } else {
      deps += [ "//third_party/openssl/ohos_lite:openssl_shared" ]
    }
    complete_static_lib = true
  }
}
